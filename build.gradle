group 'gradle-bugs'
version '1.0.0'

apply plugin: 'maven'
apply plugin: 'groovy'

repositories {
    jcenter()
}

dependencies {
    // NOTE: Anything added here should be done with explicit versions and no wildcards
    testCompile gradleApi()
    testCompile localGroovy()
    testCompile gradleTestKit()
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
        exclude module: 'groovy-all' //defer to groovy version bundled with gradle
    }
    testCompile 'org.codehaus.gpars:gpars:1.2.1'
}

test {
    // Pass through properties to use with GradleRunner based tests
    systemProperty 'target.gradle.versions',
            project.findProperty('target.gradle.versions') ?: "${gradle.gradleVersion}"
    File userHome = new File(buildDir, 'test/userHome')
    systemProperty 'target.user.home', userHome.absolutePath

    File sharedScript = file('shared.gradle')
    systemProperty 'target.shared.script', sharedScript.toURI().toURL()

    doFirst {
        // do this at task execution so a "clean" task does not delete it before
        // we need to use it
        mkdir(userHome)
    }
}
